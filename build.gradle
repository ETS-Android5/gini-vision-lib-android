// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    ext.kotlinVersion = '1.5.21'
    ext.dokkaVersion = '1.5.0'

    repositories {
        google()
        // For com.hiya:jacoco-android
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath('com.hiya:jacoco-android:0.2')
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "org.jetbrains.dokka:kotlin-as-java-plugin:$dokkaVersion"
        classpath 'com.google.gms:google-services:4.3.10'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        maven {
            // For Commons Imaging
            url 'https://repo.gini.net/nexus/content/repositories/open'
        }
        maven {
            url 'https://repo.gini.net/nexus/content/repositories/snapshots'
        }
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: rootProject.file('gradle/git_utils.gradle')

def libVersionCode = gitTagCount()
def libVersionName = "${gitDescribe()} (${libVersionCode})"

task printLibraryVersion {
    doLast {
        println "${libVersionName}"
    }
}

ext {
    compileSdkVersion = 31
    minSdkVersion = 19
    targetSdkVersion = 31
    versionCode = libVersionCode
    versionName = libVersionName
}

ext.deps = [
        // Android
        androidxCore               : 'androidx.core:core:1.3.1',
        androidxAppCompat          : 'androidx.appcompat:appcompat:1.2.0',
        androidxFragment           : 'androidx.fragment:fragment:1.2.5',
        androidxViewPager          : 'androidx.viewpager:viewpager:1.0.0',
        androidxLegacySupportV13   : 'androidx.legacy:legacy-support-v13:1.0.0',
        materialDesign             : 'com.google.android.material:material:1.2.0',
        androidxCoordinatorLayout  : 'androidx.coordinatorlayout:coordinatorlayout:1.1.0',
        androidxConstraintLayout   : 'androidx.constraintlayout:constraintlayout:1.1.0',
        androidxRecyclerView       : 'androidx.recyclerview:recyclerview:1.1.0',
        androidxCardView           : 'androidx.cardview:cardview:1.0.0',
        androidxAnnotations        : 'androidx.annotation:annotation:1.1.0',

        // Google Mobile Vision API
        playServicesVision         : 'com.google.android.gms:play-services-vision:19.0.0',
        // Exif editing library. IMPORTANT: notify backend when the version is changed
        commonsImaging             : 'org.apache.commons:commons-imaging:1.0-alpha1',
        // Logging
        slf4j                      : 'org.slf4j:slf4j-api:1.7.30',
        // Backport/standalone version of CompletableFuture
        completableFuture          : 'org.glassfish.jersey.bundles.repackaged:jersey-jsr166e:2.23.1',
        // Test dependencies
        junit                      : 'junit:junit:4.13',
        truth                      : 'com.google.truth:truth:0.28',
        mockito                    : 'org.mockito:mockito-core:2.24.5',
        mockitoAndroid             : 'org.mockito:mockito-android:2.24.5',
        mockitoKotlin              : 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0',
        androidxTestCore           : 'androidx.test:core:1.4.0',
        androidxTestJUnit          : 'androidx.test.ext:junit:1.1.3',
        androidxTestRunner         : 'androidx.test:runner:1.4.0',
        androidxTestRules          : 'androidx.test:rules:1.4.0',
        androidxTestOrchestrator   : 'androidx.test:orchestrator:1.4.0',
        androidxTestEspressoCore   : 'androidx.test.espresso:espresso-core:3.4.0',
        androidxTestEspressoIntents: 'androidx.test.espresso:espresso-intents:3.4.0',
        androidxTestUiAutomator    : 'androidx.test.uiautomator:uiautomator:2.2.0',
        androidxMultidex           : 'androidx.multidex:multidex:2.0.1',
        robolectric                : 'org.robolectric:robolectric:4.5-alpha-3'

]
