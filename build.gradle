// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    ext.kotlinVersion = '1.3.61'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath('com.dicedmelon.gradle:jacoco-android:0.1.4') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            // For Commons Imaging
            url 'https://repo.gini.net/nexus/content/repositories/open'
        }
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: rootProject.file('gradle/git_utils.gradle')

def libVersionCode = gitTagCount()
def libVersionName = "${gitDescribe()} (${libVersionCode})"

task printLibraryVersion {
    doLast {
        println "${libVersionName}"
    }
}

ext {
    compileSdkVersion = 29
    minSdkVersion = 19
    targetSdkVersion = 29
    versionCode = libVersionCode
    versionName = libVersionName
}

def supportLibVersion = '28.0.0'
def mockitoVersion = '2.24.5'
def androidxTestVersion = '1.2.0'
def espressoVersion = '3.2.0'

ext.deps = [
        // Android
        appCompatv7                : "com.android.support:appcompat-v7:$supportLibVersion",
        supportv4                  : "com.android.support:support-v4:$supportLibVersion",
        supportv13                 : "com.android.support:support-v13:$supportLibVersion",
        supportDesign              : "com.android.support:design:$supportLibVersion",
        supportCardView            : "com.android.support:cardview-v7:$supportLibVersion",
        supportAnnotations         : "com.android.support:support-annotations:$supportLibVersion",
        // Google Mobile Vision API
        playServicesVision         : 'com.google.android.gms:play-services-vision:17.0.2',
        // Exif editing library. IMPORTANT: notify backend when the version is changed
        commonsImaging             : 'org.apache.commons:commons-imaging:1.0.0gini',
        // Logging
        slf4j                      : 'org.slf4j:slf4j-api:1.7.21',
        // Backport/standalone version of CompletableFuture
        completableFuture          : 'org.glassfish.jersey.bundles.repackaged:jersey-jsr166e:2.23.1',
        // Test dependencies
        junit                      : 'junit:junit:4.12',
        truth                      : 'com.google.truth:truth:0.28',
        mockito                    : "org.mockito:mockito-core:$mockitoVersion",
        mockitoKotlin              : "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0",
        mockitoAndroid             : "org.mockito:mockito-android:$mockitoVersion",
        androidxTestCore           : "androidx.test:core:$androidxTestVersion",
        androidxTestJUnit          : 'androidx.test.ext:junit:1.1.1',
        androidxTestRunner         : "androidx.test:runner:$androidxTestVersion",
        androidxTestRules          : "androidx.test:rules:$androidxTestVersion",
        androidxTestOrchestrator   : "androidx.test:orchestrator:$androidxTestVersion",
        androidxTestEspressoCore   : "androidx.test.espresso:espresso-core:$espressoVersion",
        androidxTestEspressoIntents: "androidx.test.espresso:espresso-intents:$espressoVersion",
        androidxTestUiAutomator    : 'androidx.test.uiautomator:uiautomator:2.2.0',
        androidxMultidex           : 'androidx.multidex:multidex:2.0.0',
        robolectric                : 'org.robolectric:robolectric:4.3.1'

]
