apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitCommitUnixTime()
def appVersionName = "${version}-${gitBranch()}-${gitHash()} (${appVersionCode})"

task printVersion {
    doLast {
        println "${appVersionName}"
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "net.gini.android.vision.screenapiexample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        // Use the test runner with JUnit4 support
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
    }
}

// For testing a released version
repositories {
    // Use repo on filesystem for using local builds
    mavenLocal()
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':exampleShared')
    implementation "androidx.appcompat:appcompat:$deps.androidxAppCompat"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$deps.androidxCoordinatorLayout"
    implementation deps.androidxMultidex

    implementation 'com.karumi:dexter:6.2.3'

    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // For backward compatibility
    implementation('net.gini:gini-android-sdk:2.9.1@aar') {
        transitive = true
    }

    // For testing the local version
    implementation project(path: ':ginivision-network')
    // For testing a released version or a snapshot
//    implementation ('net.gini:gini-vision-network-lib') {
//        version {
//            strictly '4.1.0-20210701.150110-2'
//        }
//    }

    // For testing the local version
    implementation project(path: ':ginivision-accounting-network')
    // For testing a released version or a snapshot
//    implementation ('net.gini:gini-vision-accounting-network-lib') {
//        version {
//            strictly '4.1.0-20210701.150114-2'
//        }
//    }

    // For testing the local version
    implementation project(path: ':ginivision')
    // For testing a released version or a snapshot
//    implementation ('net.gini:gini-vision-lib') {
//        version {
//            strictly '4.1.0-20210701.150106-2'
//        }
//    }

    testImplementation deps.junit

    androidTestImplementation(deps.androidxTestRunner)
    androidTestImplementation deps.truth
    androidTestImplementation(deps.androidxTestRules)
    androidTestImplementation(deps.androidxTestEspressoCore)
    androidTestImplementation(deps.androidxTestEspressoIntents)
    androidTestImplementation deps.androidxTestUiAutomator
    androidTestImplementation deps.mockito
    androidTestImplementation deps.mockitoAndroid
    androidTestImplementation deps.androidxMultidex
    androidTestImplementation deps.androidxTestJUnit
}

apply from: rootProject.file('gradle/codequality.gradle')
apply from: rootProject.file('gradle/multidex_for_tests.gradle')